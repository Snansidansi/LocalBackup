plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'edu.sc.seis.launch4j' version '3.0.5'
}

group = 'com.snansidansi'
version = '0.5'

repositories {
    mavenCentral()
}

javafx {
    version = "21"
    modules = ['javafx.controls', 'javafx.fxml']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.snansidansi.app.LocalBackupApp'
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.register('fastTest', Test) {
    useJUnitPlatform {
        excludeTags 'slow'
    }
}

tasks.register('slowTest', Test) {
    useJUnitPlatform {
        includeTags 'slow'
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.snansidansi.app.LocalBackupApp'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

launch4j {
    outfile = 'LocalBackup.exe'
    mainClassName = 'com.snansidansi.app.LocalBackupApp'
    icon = "${projectDir}/src/main/resources/icons/appIcon.ico"
}